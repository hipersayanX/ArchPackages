# Maintainer: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>
# Contributor: Martchus <martchus@gmx.net>

_android_arch=

pkgname=android-${_android_arch}-boost
pkgver=1.86.0
pkgrel=1
arch=('any')
pkgdesc="Free peer-reviewed portable C++ source libraries (Android ${_android_arch})"
url="https://www.boost.org/"
license=('custom')
groups=('android-boost')
depends=("android-${_android_arch}-bzip2"
         "android-${_android_arch}-icu"
         "android-${_android_arch}-zlib"
         "android-${_android_arch}-zstd")
makedepends=('android-cmake'
             "android-${_android_arch}-openmpi")
optdepends=("android-${_android_arch}-openmpi: for mpi support")
options=(!strip !buildflags staticlibs !emptydirs)
source=("https://github.com/boostorg/boost/releases/download/boost-${pkgver}/boost-${pkgver}-cmake.tar.xz"
        '0001-Disable-wordexp.patch')
md5sums=('d02759931cedc02aded80402906c5eb6'
         'd0462761652831f7728dcc9298b1c775')

prepare() {
    cd "${srcdir}/boost-${pkgver}"

    patch -Np1 -i ../0001-Disable-wordexp.patch
}

build() {
    cd "${srcdir}/boost-${pkgver}"
    source android-env ${_android_arch}

    ./bootstrap.sh -with-toolset=gcc

    # cmake is failing to detect bzip2 in shared build, disable for now.

    #extra_options="
    #    -DBOOST_IOSTREAMS_ENABLE_BZIP2=ON
    #    -DBZIP2_INCLUDE_DIR='${ANDROID_PREFIX_INCLUDE}'
    #    -DBZIP2_LIBRARIES='${ANDROID_PREFIX_LIB}/libbz2.so'"

    android-${_android_arch}-cmake \
        -S . \
        -B build-shared \
        -DBUILD_SHARED_LIBS=ON \
        -DBOOST_ENABLE_MPI=ON \
        -DBOOST_IOSTREAMS_ENABLE_LZMA=ON \
        -DBOOST_IOSTREAMS_ENABLE_ZLIB=ON \
        -DBOOST_IOSTREAMS_ENABLE_ZSTD=ON \
        -DBOOST_LOCALE_ENABLE_ICONV=ON \
        -DBOOST_LOCALE_ENABLE_ICU=ON \
        -DLIBLZMA_INCLUDE_DIR="${ANDROID_PREFIX_INCLUDE}/lzma" \
        -DLIBLZMA_LIBRARY="${ANDROID_PREFIX_LIB}/liblzma.so" \
        -Dzstd_DIR="${ANDROID_PREFIX_LIB}/cmake/zstd" \
        -DIconv_LIBRARY="${ANDROID_PREFIX_LIB}/libiconv.so" \
        -DICU_LIBRARY="${ANDROID_PREFIX_LIB}/libiconv.so" \
        -DICU_DATA_LIBRARY_RELEASE="${ANDROID_PREFIX_LIB}/libicudata.so" \
        -DICU_I18N_LIBRARY_RELEASE="${ANDROID_PREFIX_LIB}/libicui18n.so" \
        -DICU_UC_LIBRARY_RELEASE="${ANDROID_PREFIX_LIB}/libicuuc.so" \
        -DMPI_C_HEADER_DIR="${ANDROID_PREFIX_INCLUDE}" \
        -DMPI_mpi_LIBRARY="${ANDROID_PREFIX_LIB}/libmpi.so" \
        -Wno-dev \
        ${extra_options}
    make -C build-shared $MAKEFLAGS

    # cmake Doesn't detect openmpi in static build, disable for now.

    #extra_options="
    #    -DBOOST_ENABLE_MPI=ON
    #    -DMPI_C_HEADER_DIR='${ANDROID_PREFIX_INCLUDE}'
    #    -DMPI_mpi_LIBRARY='${ANDROID_PREFIX_LIB}/libmpi.a'"

    android-${_android_arch}-cmake \
        -S . \
        -B build-static \
        -DBUILD_SHARED_LIBS=OFF \
        -DBOOST_IOSTREAMS_ENABLE_BZIP2=ON \
        -DBOOST_IOSTREAMS_ENABLE_LZMA=ON \
        -DBOOST_IOSTREAMS_ENABLE_ZLIB=ON \
        -DBOOST_IOSTREAMS_ENABLE_ZSTD=ON \
        -DBOOST_LOCALE_ENABLE_ICONV=ON \
        -DBOOST_LOCALE_ENABLE_ICU=ON \
        -DBZIP2_INCLUDE_DIR="${ANDROID_PREFIX_INCLUDE}" \
        -DBZIP2_LIBRARIES="${ANDROID_PREFIX_LIB}/libbz2.a" \
        -DLIBLZMA_INCLUDE_DIR="${ANDROID_PREFIX_INCLUDE}/lzma" \
        -DLIBLZMA_LIBRARY="${ANDROID_PREFIX_LIB}/liblzma.a" \
        -Dzstd_DIR="${ANDROID_PREFIX_LIB}/cmake/zstd" \
        -DIconv_LIBRARY="${ANDROID_PREFIX_LIB}/libiconv.a" \
        -DICU_DATA_LIBRARY_RELEASE="${ANDROID_PREFIX_LIB}/libicudata.a" \
        -DICU_I18N_LIBRARY_RELEASE="${ANDROID_PREFIX_LIB}/libicui18n.a" \
        -DICU_UC_LIBRARY_RELEASE="${ANDROID_PREFIX_LIB}/libicuuc.a" \
        -Wno-dev \
        ${extra_options}
    make -C build-static $MAKEFLAGS
}

package() {
    cd "${srcdir}/boost-${pkgver}"
    source android-env ${_android_arch}

    make -C build-shared DESTDIR="${pkgdir}" install
    make -C build-static DESTDIR="${pkgdir}" install
    ${ANDROID_STRIP} -g --strip-unneeded "${pkgdir}/${ANDROID_PREFIX_LIB}"/*.so
    ${ANDROID_STRIP} -g "${pkgdir}/${ANDROID_PREFIX_LIB}"/*.a

    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE_1_0.txt
}
