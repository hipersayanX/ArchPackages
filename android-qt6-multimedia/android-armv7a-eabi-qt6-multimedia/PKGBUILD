# Maintainer: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>
# Colaborator: Martchus <martchus@gmx.net>

_android_arch=armv7a-eabi

pkgname=android-${_android_arch}-qt6-multimedia
_qtver=6.10.2
pkgver=${_qtver/-/}
pkgrel=1
arch=('any')
url='https://www.qt.io'
license=('GPL3 LGPL3' 'FDL' 'custom')
pkgdesc="Classes for audio, video, radio and camera functionality (Android ${_android_arch})"
depends=("android-${_android_arch}-qt6-base")
optdepends=("android-${_android_arch}-qt6-declarative: QML bindings")
makedepends=('android-cmake'
             "android-${_android_arch}-qt6-declarative"
             "android-${_android_arch}-qt6-shadertools"
             'qt6-declarative'
             'qt6-shadertools'
             'ninja')
options=(!strip !buildflags staticlibs !emptydirs)
groups=('android-qt6-multimedia'
        "android-${_android_arch}-qt6")
_pkgfqn="qtmultimedia-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        '0001-Fix-compile-flags-of-resonance-audio-for-mingw-w64.patch')
md5sums=('f5043971d7b5bef9dbada183ba411362'
         '2159cc88c5829d2d1d1e2f28dd770c92')

prepare () {
    cd "${srcdir}/${_pkgfqn}"
    source android-env ${_android_arch}

    # apply patches; further descriptions can be found in patch files itself
    for patch in "${srcdir}/"*.patch; do
        msg2 "Applying patch ${patch}"
        patch -Np1 -i "${patch}" || true
    done

    if [ "${ANDROID_MINIMUM_PLATFORM}" -lt 26 ]; then
        sed -i 's|Multimedia CONDITION ANDROID|Multimedia CONDITION QT_FEATURE_aaudio|g' src/multimedia/CMakeLists.txt
        sed -i 's|defined(Q_OS_ANDROID)|QT_CONFIG(aaudio)|g' src/multimedia/platform/qplatformaudiodevices.cpp
    fi

    sed -i 's|FF_PROFILE_H264_HIGH|100|g' src/plugins/multimedia/ffmpeg/recordingengine/qffmpegencoderoptions.cpp
    sed -i 's|FF_PROFILE_HEVC_MAIN|1|g' src/plugins/multimedia/ffmpeg/recordingengine/qffmpegencoderoptions.cpp
}

build() {
    cd "${srcdir}/${_pkgfqn}"
    source android-env ${_android_arch}

    export PATH="/usr/lib/jvm/java-17-openjdk/bin:${PATH}"
    extra_params=

    if [ "${ANDROID_MINIMUM_PLATFORM}" -lt 26 ]; then
        extra_params='-DFEATURE_aaudio=OFF'
    fi

    android-${_android_arch}-cmake \
        -B build-shared \
        -S . \
        -G Ninja \
        -DCMAKE_FIND_ROOT_PATH="${ANDROID_PREFIX}" \
        -DQT_BINARY_DIR=${ANDROID_PREFIX_BIN} \
        -DQT_HOST_PATH=/usr \
        -DANDROID_SDK_ROOT=${ANDROID_HOME} \
        -DANDROID_NDK_ROOT=${ANDROID_NDK_HOME} \
        -DANDROID_STL="c++_shared" \
        -DFEATURE_pkg_config=ON \
        -DBUILD_SHARED_LIBS=ON \
        -DQT_BUILD_EXAMPLES=OFF \
        -DQT_BUILD_DOCS=OFF \
        -DFEATURE_pkg_config=ON \
        ${extra_params}
    cmake --build build-shared
}

package() {
    cd "${srcdir}/${_pkgfqn}"
    source android-env ${_android_arch}

    DESTDIR="${pkgdir}" cmake --install build-shared

    # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
    find "${pkgdir}/${ANDROID_PREFIX}/" -type f -name '*.prl' \
        -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

    rm -rvf "${pkgdir}/${ANDROID_PREFIX}/share/doc" || true
    find ${pkgdir}/${ANDROID_PREFIX} -type f -name 'lib*.so' -exec ${ANDROID_STRIP} -g --strip-unneeded {} \;
    find ${pkgdir}/${ANDROID_PREFIX} -type f -name 'lib*.a' -exec ${ANDROID_STRIP} -g {} \;

    install -Dm644 LICENSES/* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
