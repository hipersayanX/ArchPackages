# Maintainer: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>
# Colaborator: Martchus <martchus@gmx.net>

_android_arch=x86

pkgname=android-${_android_arch}-qt6-declarative
_qtver=6.10.1
pkgver=${_qtver/-/}
pkgrel=1
arch=('any')
url='https://www.qt.io'
license=('GPL3 LGPL3' 'FDL' 'custom')
pkgdesc="Classes for QML and JavaScript languages (Android ${_android_arch})"
depends=("android-${_android_arch}-qt6-base")
makedepends=('android-cmake'
             "android-${_android_arch}-qt6-shadertools"
             "android-${_android_arch}-qt6-svg"
             'qt6-declarative'
             'qt6-shadertools'
             'qt6-svg'
             'ninja'
             'python')
optdepends=('qt6-declarative: development tools')
conflicts=("android-${_android_arch}-qt6-quickcontrols2")
provides=("android-${_android_arch}-qt6-quickcontrols2")
replaces=("android-${_android_arch}-qt6-quickcontrols2")
options=(!strip !buildflags staticlibs !emptydirs)
groups=('android-qt6-declarative'
        "android-${_android_arch}-qt6")
_pkgfqn="qtdeclarative-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        '0001-Exclude-qmltime-when-cross-compiling.patch')
md5sums=('5a3cb44b944980795a3cf5c55d849cf1'
         'b24ae7735a5da9fc612173049875d829')

prepare () {
    cd "${srcdir}/${_pkgfqn}"

    # apply patches; further descriptions can be found in patch files itself
    for patch in "${srcdir}/"*.patch; do
        msg2 "Applying patch ${patch}"
        patch -Np1 -i "${patch}" || true
    done
}

build() {
    cd "${srcdir}/${_pkgfqn}"
    source android-env ${_android_arch}

    export PATH="/usr/lib/jvm/java-17-openjdk/bin:${PATH}"

    android-${_android_arch}-cmake \
        -B build-shared \
        -S . \
        -G Ninja \
        -DCMAKE_FIND_ROOT_PATH="${ANDROID_PREFIX}" \
        -DQT_BINARY_DIR=${ANDROID_PREFIX_BIN} \
        -DQT_INCLUDE_DIRS_NO_SYSTEM=ON \
        -DQT_HOST_PATH=/usr \
        -DANDROID_SDK_ROOT=${ANDROID_HOME} \
        -DANDROID_NDK_ROOT=${ANDROID_NDK_HOME} \
        -DANDROID_STL="c++_shared" \
        -DBUILD_SHARED_LIBS=ON \
        -DQT_BUILD_EXAMPLES=OFF \
        -DQT_BUILD_DOCS=OFF \
        -DFEATURE_pkg_config=ON \
        -DFEATURE_quickcontrols2_quick_designer=OFF \
        -DFEATURE_quickcontrols2_fusion=OFF \
        -DFEATURE_quickcontrols2_imagine=OFF \
        -DFEATURE_quickcontrols2_universal=OFF \
        -DFEATURE_quickcontrols2_fluentwinui3=OFF \
        -DFEATURE_quickcontrols2_macos=OFF \
        -DFEATURE_quickcontrols2_ios=OFF \
        -DFEATURE_quickcontrols2_windows=OFF
    cmake --build build-shared
}

package() {
    cd "${srcdir}/${_pkgfqn}"
    source android-env ${_android_arch}

    DESTDIR="${pkgdir}" cmake --install build-shared

    # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
    find "${pkgdir}/${ANDROID_PREFIX}/" -type f -name '*.prl' \
        -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

    rm -rvf "${pkgdir}/${ANDROID_PREFIX}/share/doc" || true
    find ${pkgdir}/${ANDROID_PREFIX} -type f -name 'lib*.so' -exec ${ANDROID_STRIP} -g --strip-unneeded {} \;
    find ${pkgdir}/${ANDROID_PREFIX} -type f -name 'lib*.a' -exec ${ANDROID_STRIP} -g {} \;

    install -Dm644 LICENSES/* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
