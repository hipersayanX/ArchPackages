# Maintainer: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>
# Colaborator: Martchus <martchus@gmx.net>

_android_arch=riscv64

pkgname=android-${_android_arch}-qt6-svg
_qtver=6.9.1
pkgver=${_qtver/-/}
pkgrel=1
arch=('any')
url='https://www.qt.io'
license=('GPL3 LGPL3' 'FDL' 'custom')
pkgdesc="Classes for displaying the contents of SVG files (Android ${_android_arch})"
depends=("android-${_android_arch}-qt6-base")
makedepends=('android-cmake'
             'qt6-base'
             'ninja')
options=(!strip !buildflags staticlibs !emptydirs)
groups=('android-qt6-svg'
        "android-${_android_arch}-qt6")
_pkgfqn="qtsvg-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('2dfc5de5fd891ff2afd9861e519bf1a26e6deb729b3133f68a28ba763c9abbd5')

build() {
    cd "${srcdir}/${_pkgfqn}"
    source android-env ${_android_arch}

    export PATH="/usr/lib/jvm/java-17-openjdk/bin:${PATH}"

    android-${_android_arch}-cmake \
        -B build-shared \
        -S . \
        -G Ninja \
        -DCMAKE_FIND_ROOT_PATH="${ANDROID_PREFIX}" \
        -DQT_BINARY_DIR=${ANDROID_PREFIX_BIN} \
        -DQT_INCLUDE_DIRS_NO_SYSTEM=ON \
        -DQT_HOST_PATH=/usr \
        -DANDROID_SDK_ROOT=${ANDROID_HOME} \
        -DANDROID_NDK_ROOT=${ANDROID_NDK_HOME} \
        -DANDROID_STL="c++_shared" \
        -DFEATURE_pkg_config=ON \
        -DBUILD_SHARED_LIBS=ON \
        -DQT_BUILD_EXAMPLES=OFF \
        -DQT_BUILD_DOCS=OFF \
        -DFEATURE_pkg_config=ON
    VERBOSE=1 cmake --build build-shared
}

package() {
    cd "${srcdir}/${_pkgfqn}"
    source android-env ${_android_arch}

    DESTDIR="${pkgdir}" cmake --install build-shared

    install -d "${pkgdir}/usr/share/licenses"
    ln -s "/usr/share/licenses/android-${_android_arch}-qt6-base" "${pkgdir}/usr/share/licenses/${pkgname}"

    # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
    find "${pkgdir}/${ANDROID_PREFIX}/" -type f -name '*.prl' \
        -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

    rm -rvf "${pkgdir}/${ANDROID_PREFIX}/share/doc" || true
    find ${pkgdir}/${ANDROID_PREFIX} -type f -name 'lib*.so' -exec ${ANDROID_STRIP} -g --strip-unneeded {} \;
    find ${pkgdir}/${ANDROID_PREFIX} -type f -name 'lib*.a' -exec ${ANDROID_STRIP} -g {} \;
}
